buildscript {
    ext.kotlin_version = '2.1.0'
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:8.7.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.google.gms:google-services:4.4.0'
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }
    // Force AndroidX versions compatible with AGP 8.7.x (avoids AAR metadata requiring 8.9.1+)
    configurations.all {
        resolutionStrategy {
            force 'androidx.core:core:1.15.0'
            force 'androidx.core:core-ktx:1.15.0'
            force 'androidx.activity:activity:1.9.0'
            force 'androidx.activity:activity-ktx:1.9.0'
            force 'androidx.browser:browser:1.8.0'
            force 'androidx.exifinterface:exifinterface:1.3.7'
        }
    }
}

rootProject.buildDir = '../build'
subprojects {
    project.buildDir = "${rootProject.buildDir}/${project.name}"
}
subprojects {
    project.evaluationDependsOn(':app')
    afterEvaluate {
        if (project.hasProperty('android')) {
            project.android {
                lint {
                    disable "NullSafeMutableLiveData"
                    checkReleaseBuilds false
                }
            }
        }
    }
}

tasks.register("clean", Delete) {
    delete rootProject.buildDir
}
